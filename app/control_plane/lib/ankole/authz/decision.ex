defmodule Ankole.AuthZ.Decision do
  @moduledoc false

  alias Ankole.AuthZ.Snapshot
  alias Ankole.Kernel, as: AnkoleKernel
  alias Ankole.Logging
  alias Ankole.Repo

  def authorize_decision(principal_uid, resource, action, context \\ %{}) do
    authorize_decision(Repo, principal_uid, resource, action, context)
  end

  def authorize_decision(repo, principal_uid, resource, action, context) do
    with {:ok, snapshot} <-
           Snapshot.build_authorization_snapshot(repo, principal_uid, resource, action, context),
         {:ok, decision} <- kernel_decision(AnkoleKernel.authz_authorize(snapshot)) do
      emit_diagnostics(decision)
      {:ok, decision}
    end
  end

  def authorize_all_decision(principal_uid, resource, actions, context \\ %{}) do
    authorize_all_decision(Repo, principal_uid, resource, actions, context)
  end

  def authorize_all_decision(repo, principal_uid, resource, actions, context) do
    with {:ok, snapshot} <-
           Snapshot.build_authorization_batch_snapshot(repo, principal_uid, resource, actions, context),
         {:ok, decision} <- kernel_decision(AnkoleKernel.authz_authorize_all(snapshot)) do
      emit_diagnostics(decision)
      {:ok, decision}
    end
  end

  def result(%{"status" => "allow"}), do: :ok
  def result(%{"status" => "principal_disabled"}), do: {:error, :principal_disabled}
  def result(%{"status" => "invalid_request"}), do: {:error, :invalid_request}

  def result(%{"status" => "deny", "deniedAction" => action}),
    do: {:error, {:forbidden, action}}

  def result(%{"status" => "deny"}), do: {:error, :forbidden}
  def result(_decision), do: {:error, :invalid_decision}

  @doc false
  def preview_computed_membership?(snapshot, group_id) do
    case kernel_decision(AnkoleKernel.authz_authorize(snapshot)) do
      {:ok, decision} ->
        emit_diagnostics(decision)
        group_id in Map.get(decision, "effectiveGroupIDs", [])

      _error ->
        false
    end
  end

  defp kernel_decision(%{} = decision), do: {:ok, decision}
  defp kernel_decision(_decision), do: {:error, :invalid_decision}

  defp emit_diagnostics(%{"diagnostics" => diagnostics}) when is_list(diagnostics) do
    Enum.each(diagnostics, &emit_diagnostic/1)
  end

  defp emit_diagnostics(_decision), do: :ok

  defp emit_diagnostic(%{} = diagnostic) do
    metadata = %{
      kind: diagnostic["kind"],
      id: diagnostic["id"],
      action: diagnostic["action"],
      resource_pattern: diagnostic["resourcePattern"],
      reason: diagnostic["reason"]
    }

    Logging.error("authz.invalid_persisted_data", "AuthZ invalid persisted data", metadata)
    :telemetry.execute([:ankole, :authz, :invalid_persisted_data], %{count: 1}, metadata)
  end

  defp emit_diagnostic(_diagnostic), do: :ok
end
